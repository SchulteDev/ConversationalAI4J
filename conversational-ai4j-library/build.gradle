plugins {
  id 'java-library'
  id 'maven-publish'
}

java {
  withSourcesJar()
  withJavadocJar()
}

configurations {
  mockitoAgent
}

ext.libs = [mockito: 'org.mockito:mockito-core:5.21.0']

dependencies {
  // LangChain4j with Ollama
  implementation 'dev.langchain4j:langchain4j:1.8.0'
  implementation 'dev.langchain4j:langchain4j-ollama:1.8.0'

  // Logging
  implementation 'org.slf4j:slf4j-api:2.0.17'
  runtimeOnly 'org.slf4j:slf4j-simple:2.0.17'

  // Speech functionality via Whisper.cpp and Piper JNI
  implementation 'io.github.givimad:whisper-jni:1.7.1'
  implementation 'io.github.givimad:piper-jni:1.2.0-c0670df'

  // Test dependencies
  testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
  testImplementation libs.mockito
  testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
  mockitoAgent(libs.mockito) { transitive = false }
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'
}

// Note: Whisper and Piper JNI libraries include native binaries
// Cross-platform support: Windows, Linux, macOS (x86_64/arm64)

test {
  useJUnitPlatform()
  // Mockito agent configuration
  jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}


